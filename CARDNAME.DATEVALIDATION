[  <TITLE>
|  CARDNAME.DATEVALIDATION
|
|  <PURPOSE>
|  Assist clients with detailed information regarding errors received
|  due to the "No Valid Name Found" exception. This exception can occur
|  for one of following reasons:
|  * Member is declared deceased
|  * Name Record is expired
|  * Name Record is not yet effective
|
|  <ANY SPECIAL INFORMATION/NOTES REQUIRED TO RUN?>
|  Clients should review their card extract edits and synchronize their
|  prompt answers to match.
|
|  <Stack comments in reverse chronological order.>
|
|  PROGRAMMER:  M. Schumacher, Symitar
|  DATE:        2016-DEC-22
|  RELEASE:     2016.01
|  CASE/DEFECT: #######/1114666
|  DETAILS:     Created specfile.
]

TARGET=CARD

DEFINE
 #INCLUDE "RB.LISTEXPAND.DEF"
 [constants]
 TRUE=1
 FALSE=0
 
 [enumerations]
 CARDSTATUSNOTISSUED=0
 CARDSTATUSISSUED=1
 CARDSTATUSCAPTURED=2

 CARDNAMETYPEPRIMARY=0
 CARDNAMETYPEJOINT=1
 CARDNAMETYPEMAIL=2

 [variables]
 I=NUMBER
 J=NUMBER
 ACCTNUM=CHARACTER(10)
 CARDNUM=CHARACTER(20)
 NUMCARDS=NUMBER ARRAY(2,2)
 TOTCARDS=NUMBER
 VALIDNAME=NUMBER
 ANS=CHARACTER(1)
 ALLOWNAMETYPE1=NUMBER
 REASON=CHARACTER(75)
 ERROR=NUMBER
 
 EXPDATE=DATE
 DEATHDATE=DATE
 EFFECTDATE=DATE
 NAMELOC=NUMBER
 INCLCLOSEDACCTS=NUMBER
 ALLOWCARDSTATUS=NUMBER ARRAY(2)
 CARDSTATUS=NUMBER
END

SETUP
 LELISTINPUT=CHARACTERREAD("Card Type List")
 CALL LISTEXPAND

 IF YESNOREAD("Include Closed Accounts") THEN
  INCLCLOSEDACCTS=TRUE
 ELSE
  INCLCLOSEDACCTS=FALSE
 
 ALLOWNAMETYPE1=FALSE
 IF YESNOREAD("Allow Name Type 1 Joint") THEN
  ALLOWNAMETYPE1=TRUE

 IF YESNOREAD("Include Not Issued Cards") THEN
  ALLOWCARDSTATUS(0)=TRUE
 ELSE
  ALLOWCARDSTATUS(0)=FALSE

 IF YESNOREAD("Include Issued Cards") THEN
  ALLOWCARDSTATUS(1)=TRUE
 ELSE
  ALLOWCARDSTATUS(1)=FALSE
  
 IF YESNOREAD("Include Captured Cards") THEN
  ALLOWCARDSTATUS(2)=TRUE
 ELSE
  ALLOWCARDSTATUS(2)=FALSE

 TOTCARDS=0
 FOR I=0 TO 2
 DO
  FOR J=0 TO 2
  DO
   NUMCARDS(I,J)=0
  END
 END
END

SELECT
 LELIST(CARD:TYPE)=TRUE AND
 (ACCOUNT:CLOSEDATE='--/--/--' OR INCLCLOSEDACCTS=TRUE) AND
 ALLOWCARDSTATUS(CARD:STATUS)=TRUE
END

PRINT TITLE="Card Name DB Integrity Violations"
 HEADERS
  COL=01 "Card Number"
  COL=25 "Account"
  COL=40 "Status"
  COL=55 "Reason"
  NEWLINE
  PRINT REPEATCHR("-",132)
  NEWLINE
 END

 ACCTNUM=ACCOUNT:NUMBER
 CARDNUM=CARD:NUMBER
 IF CARD:SUFFIX="" THEN
  CARDNUM=CARD:NUMBER+"-"+CARD:SUFFIX
 VALIDNAME=FALSE
 ERROR=FALSE
 CARDSTATUS=CARD:STATUS

 IF CARD:NAMETYPE=CARDNAMETYPEPRIMARY THEN
  DO
   VALIDNAME=TRUE
   FOR ACCOUNT ACCTNUM
    DO
     FOR EACH NAME WITH (NAME:TYPE=0)
      DO
       NAMELOC=NAME:LOCATOR
       DEATHDATE=NAME:DEATHDATE
       EXPDATE=NAME:EXPIRATIONDATE
       EFFECTDATE=NAME:EFFECTIVEDATE
       CALL VALIDATENAMEDATES
      END
    END
  END
 ELSE IF CARD:NAMETYPE=CARDNAMETYPEJOINT THEN 
  DO
   FOR ACCOUNT ACCTNUM
    DO
     FOR EACH NAME WITH (NAME:TYPE=1)
      DO
       VALIDNAME=TRUE
       NAMELOC=NAME:LOCATOR
       DEATHDATE=NAME:DEATHDATE
       EXPDATE=NAME:EXPIRATIONDATE
       EFFECTDATE=NAME:EFFECTIVEDATE
      END
     UNTIL VALIDNAME=TRUE
     IF VALIDNAME=FALSE AND ALLOWNAMETYPE1=TRUE THEN
      DO
       ERROR=TRUE
       REASON="No Joint Name At Account Level"
       CALL PRINTREASON
      END
     ELSE IF VALIDNAME=TRUE AND ALLOWNAMETYPE1=FALSE THEN
      DO
       ERROR=TRUE
       REASON="Joint Name At Account Level Not allowed"
       CALL PRINTREASON
      END
     ELSE IF VALIDNAME=TRUE AND ALLOWNAMETYPE1=TRUE THEN
      DO
       CALL VALIDATENAMEDATES
      END
    END
  END
 ELSE IF CARD:NAMETYPE=CARDNAMETYPEMAIL THEN
  DO
   FOR EACH CARD NAME
    DO
     VALIDNAME=TRUE
     NAMELOC=CARD NAME:LOCATOR
     DEATHDATE=CARD NAME:DEATHDATE
     EXPDATE=CARD NAME:EXPIRATIONDATE
     EFFECTDATE=CARD NAME:EFFECTIVEDATE
    END
   UNTIL VALIDNAME=TRUE
   IF VALIDNAME=FALSE THEN
    DO
     ERROR=TRUE
     REASON="No Name At Card Name Level"
     CALL PRINTREASON
    END
   ELSE
    DO
     CALL VALIDATENAMEDATES
    END
  END

 IF ERROR=TRUE THEN
  DO
   [now tally results]
   TOTCARDS=TOTCARDS+1
   I=CARD:NAMETYPE
   J=CARD:STATUS
   NUMCARDS(I,J)=NUMCARDS(I,J)+1
  END
END

TOTAL
 NEWLINE
 PRINT REPEATCHR("=",132)
 NEWLINE
 COL=01 "Name Type"
 COL=14 "Not Issued"
 COL=31 "Issued"
 COL=45 "Captured"
 NEWLINE
 PRINT REPEATCHR("-",55)
 NEWLINE
 COL=01 "Primary"
 COL=10 "|"
 COL=24 RIGHT NUMCARDS(CARDNAMETYPEPRIMARY,CARDSTATUSNOTISSUED)
 COL=25 "|"
 COL=39 RIGHT NUMCARDS(CARDNAMETYPEPRIMARY,CARDSTATUSISSUED)
 COL=40 "|"
 COL=54 RIGHT NUMCARDS(CARDNAMETYPEPRIMARY,CARDSTATUSCAPTURED)
 COL=55 "|"
 NEWLINE
 PRINT REPEATCHR("-",55)
 NEWLINE
 COL=01 "1st Joint"
 COL=10 "|"
 COL=24 RIGHT NUMCARDS(CARDNAMETYPEJOINT,CARDSTATUSNOTISSUED)
 COL=25 "|"
 COL=39 RIGHT NUMCARDS(CARDNAMETYPEJOINT,CARDSTATUSISSUED)
 COL=40 "|"
 COL=54 RIGHT NUMCARDS(CARDNAMETYPEJOINT,CARDSTATUSCAPTURED)
 COL=55 "|"
 NEWLINE
 PRINT REPEATCHR("-",55)
 NEWLINE
 COL=01 "Subrecord"
 COL=10 "|"
 COL=24 RIGHT NUMCARDS(CARDNAMETYPEMAIL,CARDSTATUSNOTISSUED)
 COL=25 "|"
 COL=39 RIGHT NUMCARDS(CARDNAMETYPEMAIL,CARDSTATUSISSUED)
 COL=40 "|"
 COL=54 RIGHT NUMCARDS(CARDNAMETYPEMAIL,CARDSTATUSCAPTURED)
 COL=55 "|"
 NEWLINE
 PRINT REPEATCHR("-",55)
 NEWLINE
 NEWLINE
 NEWLINE
 PRINT TOTCARDS
 PRINT " Cards Affected"
 NEWLINE
END

#INCLUDE "RB.LISTEXPAND"

PROCEDURE PRINTREASON
 COL=01 CARDNUM
 COL=25 ACCTNUM
 IF CARDSTATUS=0 THEN
  COL=40 "Not Issued"
 ELSE IF CARDSTATUS=1 THEN
  COL=40 "Issued"
 ELSE IF CARDSTATUS=2 THEN
  COL=40 "Captured"
 COL=55 REASON
 NEWLINE
END

PROCEDURE VALIDATENAMEDATES
 IF DEATHDATE<>'--/--/--' THEN
  DO
   ERROR=TRUE
   REASON="Locator "+FORMAT("########9",NAMELOC)+": Member Is Deceased"
   CALL PRINTREASON
  END
 IF EFFECTDATE>SYSTEMDATE THEN 
  DO
   ERROR=TRUE
   REASON="Locator "+FORMAT("########9",NAMELOC)+
          ": Record Is Not Effective"
   CALL PRINTREASON
  END  
 IF EXPDATE<>'--/--/--' AND EXPDATE<=SYSTEMDATE THEN 
  DO
   ERROR=TRUE
   REASON="Locator "+FORMAT("########9",NAMELOC)+": Record Is Expired"
   CALL PRINTREASON
  END 
END
