[SINGLE.INDIRECT.LOAN.ONLY.V2]

TARGET=ACCOUNT

DEFINE
 TYP=NUMBER      ARRAY(99)
 ID=CHARACTER(4) ARRAY(99)
 BAL=MONEY       ARRAY(99)
 CHOFF=DATE      ARRAY(99)
 OPNDATE=DATE    ARRAY(99)
 CLSDATE=DATE    ARRAY(99)
 LSTPMDATE=DATE  ARRAY(99)
 CNT=NUMBER
 TOT=NUMBER
 X=NUMBER
 FLAG=NUMBER
 SHCNT=NUMBER
 SHOPEN=DATE
 SHCLOSE=DATE
 SHBAL=MONEY
 LSTTRANDATE=DATE
 INACT=NUMBER
 NOPRIM=NUMBER
 WARN=NUMBER
 INACTCNT=NUMBER
 NOPRIMCNT=NUMBER
 WARNCNT=NUMBER
 CASHCNT=NUMBER
 MINCASHCNT=NUMBER
 TOTCNT=NUMBER
 OPENTOT=NUMBER
 OPENTOT2=NUMBER
 INACTONLYCNT=NUMBER
 INACTCASHCNT=NUMBER
 LNBALCNT=NUMBER
 CHOFFCNT=NUMBER
 MULTCNT=NUMBER
 INDLCNT=NUMBER
END

SETUP
 CNT=0
 TOT=0
 FLAG=0
 SHCNT=0
 INACT=0
 WARN=0
 INACTCNT=0
 NOPRIM=0
 NOPRIMCNT=0
 WARNCNT=0
 CASHCNT=0
 MINCASHCNT=0
 INACTONLYCNT=0
 INACTCASHCNT=0
 LNBALCNT=0
 CHOFFCNT=0
 TOTCNT=0
 OPENTOT=0
 OPENTOT2=0
 MULTCNT=0
 INDLCNT=0
 SHBAL=$0.00
 SHOPEN='--/--/--'
 SHCLOSE='--/--/--'
 LSTTRANDATE='--/--/--'
 FOR X=0 TO 99
  DO
   TYP(X)=0
   ID(X)=""
   BAL(X)=$0.00
   CHOFF(X)='--/--/--'
   OPNDATE(X)='--/--/--'
   CLSDATE(X)='--/--/--'
   LSTPMDATE(X)='--/--/--'
  END
 X=0
END

SELECT
 ANY LOAN WITH (((LOAN:TYPE>=09 AND LOAN:TYPE<=10) OR [MOTORCYCLE]
  (LOAN:TYPE>=33 AND LOAN:TYPE<=34) OR [RV]
  (LOAN:TYPE=43) OR                     [PM2]
  (LOAN:TYPE>=56 AND LOAN:TYPE<=59)) OR [AUTO]
 ((LOAN:COLLATERALCODE>=09 AND LOAN:COLLATERALCODE<=10) OR [MOTORCYCLE] 
  (LOAN:COLLATERALCODE>=33 AND LOAN:COLLATERALCODE<=34) OR [RV]
  (LOAN:COLLATERALCODE=43) OR
  (LOAN:COLLATERALCODE>=56 AND LOAN:COLLATERALCODE<=59) OR [AUTO]
 ((LOAN:PURPOSECODE>=09 AND LOAN:PURPOSECODE<=10) OR [MOTORCYCLE]
  (LOAN:PURPOSECODE>=33 AND LOAN:PURPOSECODE<=34) OR [RV]
  (LOAN:PURPOSECODE=43) OR                           [PM2]
  (LOAN:PURPOSECODE>=56 AND LOAN:PURPOSECODE<=61)))) [AUTO]
END

SORT
 LOAN:CLOSEDATE
END

PRINT TITLE="Single Indirect Loans Only"
 HEADERS
  COL=001 "Account #"
  COL=012 "Name"
  COL=034 RIGHT "Type"
  COL=038 "ID"
  COL=050 RIGHT "Ln Bal"
  COL=060 RIGHT "ChgOff"
  COL=071 RIGHT "ShOpen"
  COL=082 RIGHT "LnOpen"
  COL=093 RIGHT "LnClose"
  COL=104 RIGHT "LstPmt"
  COL=115 RIGHT "LstTrn" 
  COL=117 "INA"
  COL=121 "PRI"
  COL=131 "SD"
  NEWLINE
  COL=001 REPEATCHR("-",60)
  NEWLINE
 END
 CNT=0
 FLAG=0
 SHCNT=0
 INACT=0
 WARN=0
 NOPRIM=0
 SHBAL=$0.00
 SHOPEN='--/--/--'
 SHCLOSE='--/--/--'
 LSTTRANDATE='--/--/--'
 FOR X=0 TO 99
  DO
   TYP(X)=0
   ID(X)=""
   BAL(X)=$0.00
   CHOFF(X)='--/--/--'
   OPNDATE(X)='--/--/--'
   CLSDATE(X)='--/--/--'
   LSTPMDATE(X)='--/--/--'
  END
  [--------------###ALL INDIRECT LOANS /START###-------------------]
 FOR EACH LOAN WITH (((LOAN:TYPE>=09 AND LOAN:TYPE<=10) OR [MOTORCYCLE]
  (LOAN:TYPE>=33 AND LOAN:TYPE<=34) OR [RV]
  (LOAN:TYPE=43) OR                     [PM2]
  (LOAN:TYPE>=56 AND LOAN:TYPE<=59)) OR [AUTO]
 ((LOAN:COLLATERALCODE>=09 AND LOAN:COLLATERALCODE<=10) OR [MOTORCYCLE] 
  (LOAN:COLLATERALCODE>=33 AND LOAN:COLLATERALCODE<=34) OR [RV]
  (LOAN:COLLATERALCODE=43) OR
  (LOAN:COLLATERALCODE>=56 AND LOAN:COLLATERALCODE<=59) OR [AUTO]
 ((LOAN:PURPOSECODE>=09 AND LOAN:PURPOSECODE<=10) OR [MOTORCYCLE]
  (LOAN:PURPOSECODE>=33 AND LOAN:PURPOSECODE<=34) OR [RV]
  (LOAN:PURPOSECODE=43) OR                           [PM2]
  (LOAN:PURPOSECODE>=56 AND LOAN:PURPOSECODE<=61)))) [AUTO]
  DO
   TOTCNT=TOTCNT+1            [---TOTAL # OF INDIRECT LOANS---]
   CNT=CNT+1                  [---INDL ON ACCOUNT INDEXING---]
   TYP(CNT)=LOAN:TYPE
   ID(CNT)=LOAN:ID
   BAL(CNT)=LOAN:BALANCE
   CHOFF(CNT)=LOAN:CHARGEOFFDATE
   OPNDATE(CNT)=LOAN:OPENDATE
   CLSDATE(CNT)=LOAN:CLOSEDATE
   LSTPMDATE(CNT)=LOAN:LASTPAYMENTDATE
   IF (CHOFF(CNT)='--/--/--') THEN
    DO
	 TOT=TOT+1               [---NON CHARGEOFF INDL COUNTER---]
	 IF (BAL(CNT)>0) THEN
	  DO
	   OPENTOT=OPENTOT+1     [---NON CHARGEOFF POS BALANCE INDL COUNTER---]
	  END
	 IF CLSDATE(CNT)='--/--/--' THEN
	  DO
	   OPENTOT2=OPENTOT2+1   [---NON CHARGEOFF OPEN INDL COUNTER----]
	  END
	END
	IF (CHOFF(CNT)<>'--/--/--') THEN
	 DO
	  CHOFFCNT=CHOFFCNT+1   [---CHARGEOFF INDL COUNTER---]
	 END
  END[-----------------###ALL INDIRECT LOANS /END###-----------------]
  
  
  [-----------###SET FLAGS /BEGIN###------------------]
  IF CNT>1 THEN
   DO
    MULTCNT=MULTCNT+1           [----MORE THAN ONE INDL ON ACCOUNT-------]
	FLAG=1                      [----SET A FLAG FOR MULTIPLE INDL ON ACCOUNT----]
   END
  FOR EACH LOAN WITH ((LOAN:TYPE<>09 AND
 			LOAN:TYPE<>10 AND
 			LOAN:TYPE<>33 AND
 			LOAN:TYPE<>34 AND
 			LOAN:TYPE<>43 AND
 			LOAN:TYPE<>56 AND
 			LOAN:TYPE<>57 AND
 			LOAN:TYPE<>58 AND
 			LOAN:TYPE<>59 AND
 			LOAN:TYPE<>87 AND
 			LOAN:TYPE<>88 AND
                LOAN:TYPE=99) OR
                CHARACTERSEARCH(LOAN:DESCRIPTION,"ERROR")>0)
  DO
   FLAG=1        [------------SET FLAG FOR OTHER LOANS ON ACCOUNT--------]
  END
  FOR EACH SHARE 
   DO
    IF (SHARE:ID<>"00") THEN
      FLAG=1         [----SET FLAG OTHER SHARES ON ACCOUNT-----------]
    IF (SHARE:ID="00") THEN
     DO
      SHOPEN=SHARE:OPENDATE
      SHCLOSE=SHARE:CLOSEDATE
      SHBAL=SHARE:BALANCE
      LSTTRANDATE=SHARE:LASTTRANDATE
      IF (OPNDATE(CNT)>=DATEOFFSET(SHARE:OPENDATE,0,30) OR
	      OPNDATE(CNT)<=DATEOFFSET(SHARE:OPENDATE,0,-30) OR
          SHCLOSE<>'--/--/--' OR
          (CHARACTERSEARCH(SHARE NAME:LAST,"IN ERROR")>0) OR
          (CHARACTERSEARCH(NAME:LAST,"IN ERROR")>0) OR
          SHARE:TYPE=99) THEN
       DO
	   FLAG=1    [---SET FLAG FOR MORE THAN 30 DAYS WITHIN OPENING OR ERROR---]
       END
      IF (CHOFF(CNT)<>'--/--/--') THEN
       DO
		FLAG=1                  [---SET A FLAG FOR CHARGEOFF LOANS--------]
       END
      IF (BAL(CNT)>0 AND SHCLOSE<>'--/--/--') THEN
       DO
	    FLAG=1
        WARN=1                 [----OPEN LOAN BUT NO SHARE (SHOULDNT HAPPEN)-----]
		WARNCNT=WARNCNT+1
       END
	  IF SHOPEN='--/--/--' THEN
		DO
            NOPRIM=1
		 NOPRIMCNT=NOPRIMCNT+1             [---INDL GROUP: NO PRIMARY-----]
		END
	[---------------###SET FLAGS /END###------------------]
	  IF (BAL(CNT)=0 AND 
         CLSDATE(CNT)<>'--/--/--' AND
         LSTPMDATE(CNT)=SHARE:LASTTRANDATE) THEN
       DO
	    INACT=1              [---CLOSED LOAN WITH AN INACTIVE SHARE----------]
        INACTCNT=INACTCNT+1   [-----COUNTER------]
       END
	  IF SHBAL>$25.00 THEN
		DO
		 CASHCNT=CASHCNT+1    [----PRIM SHARE ABOVE $25-------]
		 IF (LSTPMDATE(CNT)=SHARE:LASTTRANDATE AND 
			 CLSDATE(CNT)<>'--/--/--') THEN
		  DO
		   INACTCASHCNT=INACTCASHCNT+1    [---CLOSED LOAN WITH INACTIVE SHARE ABOVE $25---]
		  END
		END
	 END           [----END IF SHARE ID 00----]
    END            [----END FOR EACH SHARE---]

	
 IF (CNT=1 AND FLAG=0 AND CHARACTERSEARCH(NAME:LAST,"IN ERROR")=0) THEN    
 [---ONE INDIRECT LOAN ONLY, OPENED WITHIN 30 DAYS OF MEMBERSHIP, NO CO, NO ERROR---------]
  DO
   INDLCNT=INDLCNT+1                    [--INDL GROUP: ONE INDL ONLY, OPN 30 DAYS OF MEM, NO CO COUNTER----]
   COL=001 ACCOUNT:NUMBER
   COL=012 NAME:SHORTNAME
   COL=034 TYP(1)
   COL=038 ID(1)
   COL=050 BAL(1)
   COL=060 CHOFF(1)
   COL=071 SHOPEN
   COL=082 OPNDATE(1)
   COL=093 CLSDATE(1)
   COL=104 LSTPMDATE(1)
   COL=115 LSTTRANDATE
   IF INACT=1 THEN
   COL=117 "INA"
   IF SHOPEN='--/--/--' THEN 
    DO
     COL=120 "X"
     NOPRIMCNT=NOPRIMCNT+1
    END
  END
   IF SHBAL<=$25.00 THEN
    DO
     MINCASHCNT=MINCASHCNT+1             [---INDL GROUP: 25$ MINIMUM IN PRIMARY---]
     IF (INACT=1) THEN
      DO
       INACTONLYCNT=INACTONLYCNT+1       [---INDL GROUP: CLOSED INACTIVE WITH $25 MIN IN PRIMARY---]
      END
    END  
   IF BAL(CNT)>0 THEN
    DO
     LNBALCNT=LNBALCNT+1                 [---INDL GROUP: POS BALANCE LOAN---]
    END  
END

TOTAL
 NEWLINE
 PRINT "Total # of Indirect Loans:"
 COL=100 TOTCNT
 NEWLINE
  PRINT "Total # of Indirect Loans, excluding Chargeoff:"
 COL=100 TOT
 NEWLINE
 PRINT "Total # of Open INDL Loans, excluding CO(+BAL):"
 COL=100 OPENTOT
 NEWLINE
 PRINT "tOTAL # of Open INDL Loans, excluding CO(OPN):" 
 COL=100 OPENTOT2
 NEWLINE
 PRINT "Total # of Chargeoff's:"
 COL=100 CHOFFCNT
 NEWLINE
 PRINT "Total # of Members with multiple Indirect Loans:"
 COL=100 MULTCNT
 NEWLINE
 PRINT "Total # of Members without Primary Share:"
 COL=100 NOPRIMCNT
 NEWLINE
 PRINT "Total # of Closed Loans w/ Inactive Share"
 COL=100 INACTCNT
 NEWLINE
 PRINT "Total # of Prim Shares above $25 minimum:"
 COL=100 CASHCNT
 NEWLINE
 PRINT "Total # of Inactive at or below $25 or no Primary:"
 COL=100 INACTCASHCNT
 NEWLINE 
 NEWLINE
 PRINT "INDL GROUP: ONE INDIRECT LOAN ONLY, OPENED WITHIN 30 DAYS OF MEMBERSHIP, NO CO'S"
 COL=100 INDLCNT
 NEWLINE
 PRINT "INDL GROUP: POS BALANCE LOAN"
 COL=100 LNBALCNT
 NEWLINE
 PRINT "INDL GROUP: 25$ MINIMUM IN PRIMARY"
 COL=100 MINCASHCNT
 NEWLINE
 PRINT "INDL GROUP: CLOSED INACTIVE WITH $25 MIN IN PRIMARY"
 COL=100 INACTONLYCNT
 NEWLINE
END
